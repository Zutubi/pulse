!include env.nsh

;--------------------------------
;Include Modern UI

  !include "MUI.nsh"

;--------------------------------
;Variables

  Var STARTMENU_FOLDER

;--------------------------------
;General

  ;Name and file
  Name "Zutubi __SERVICE_NAME__ __VERSION__"
  OutFile "..\__PACKAGE_NAME__.exe"

  ;Default installation folder
  InstallDir "$PROGRAMFILES\Zutubi\__SERVICE_NAME__ __VERSION__"
  
  ;Get installation folder from registry if available
  InstallDirRegKey HKCU "Software\Zutubi\__SERVICE_NAME__ __VERSION__" ""

;--------------------------------
;Interface Settings

  !define MUI_ICON "..\..\..\installer\pulse.ico"
  !define MUI_UNICON "..\..\..\installer\pulse.ico"
  !define MUI_HEADERIMAGE
  !define MUI_HEADERIMAGE_BITMAP "..\..\..\installer\pulse-banner.bmp"
  !define MUI_ABORTWARNING

;--------------------------------
;Pages

;  !insertmacro MUI_PAGE_LICENSE "${NSISDIR}\Docs\Modern UI\License.txt"
;  !insertmacro MUI_PAGE_COMPONENTS
  Page custom EnsureJRE
  !insertmacro MUI_PAGE_DIRECTORY

  !define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKCU" 
  !define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\Zutubi\__SERVICE_NAME__ __VERSION__"
  !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"
  
  !insertmacro MUI_PAGE_STARTMENU Application $STARTMENU_FOLDER

  !insertmacro MUI_PAGE_INSTFILES
  
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  
;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"

;--------------------------------
; Functions

Function InstallJRE
  DetailPrint "Downloading the JRE setup"
  NSISdl::download /TIMEOUT=30000 http://zutubi.com/download/jre.exe "$TEMP\pulse_jre_setup.exe"
  Pop $0 ;Get the return value
  StrCmp $0 "success" RunInstall 0
  StrCmp $0 "cancel" 0 +3
  Push "Download cancelled."
  Goto Error
  Push "Unknown error during download."
  Goto Error

RunInstall:
  DetailPrint "Launching JRE setup"
  ExecWait "$TEMP\pulse_jre_setup.exe" $0
  DetailPrint "Setup finished"
  Delete "$TEMP\pulse_jre_setup.exe"
  StrCmp $0 "0" Exit 0
  Push "The JRE setup has been abnormally interrupted."
  Goto Error

Error:
  Pop $2
  MessageBox MB_OK "Unable to install Java runtime: $2"
  Quit
Exit:
FunctionEnd

Function EnsureJRE
  ReadEnvStr $0 JAVA_HOME
  StrCmp $0 "" 0 Exit
  MessageBox MB_YESNO "No Java runtime installed or JAVA_HOME not set.  Download and install Java now?" IDYES 0 IDNO Exit
  Call InstallJRE
Exit:
FunctionEnd

;--------------------------------
;Installer Sections

Section "Pulse" SecMain
  SetOutPath "$INSTDIR"
  
  Push "PULSE_HOME"
  Push "$INSTDIR"
  Call WriteEnvStr

  File /r /x pulse.nsi /x env.nsh *
  
  Exec '"$INSTDIR\bin\install-service.bat"'

  ;Store installation folder
  WriteRegStr HKCU "Software\Zutubi\__SERVICE_NAME__ __VERSION__" "" $INSTDIR
  
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    CreateDirectory "$SMPROGRAMS\$STARTMENU_FOLDER"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Start Service.lnk" "$INSTDIR\bin\start-service.bat" "" "$INSTDIR\pulse.ico" 0
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Stop Service.lnk" "$INSTDIR\bin\stop-service.bat" "" "$INSTDIR\pulse.ico" 0
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Uninstall.lnk" "$INSTDIR\Uninstall.exe" "" "$INSTDIR\Uninstall.exe" 0
  !insertmacro MUI_STARTMENU_WRITE_END

  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"

SectionEnd

;--------------------------------
;Descriptions

  ;Language strings
  LangString DESC_SecMain ${LANG_ENGLISH} "Install pulse."

  ;Assign language strings to sections
  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${SecMain} $(DESC_SecMain)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END

;--------------------------------
;Uninstaller Section

Section "un.Uninstall"

  Push "PULSE_HOME"
  Call un.DeleteEnvStr

  Exec '"$INSTDIR\bin\uninstall-service.bat"'

  !insertmacro MUI_STARTMENU_GETFOLDER Application $STARTMENU_FOLDER
  DetailPrint "$STARTMENU_FOLDER"

  Delete "$SMPROGRAMS\$STARTMENU_FOLDER\Start Service.lnk"
  Delete "$SMPROGRAMS\$STARTMENU_FOLDER\Stop Service.lnk"
  Delete "$SMPROGRAMS\$STARTMENU_FOLDER\Uninstall.lnk"
  RMDir "$SMPROGRAMS\$STARTMENU_FOLDER"

  RMDir /r "$INSTDIR"

  DeleteRegKey /ifempty HKCU "Software\Zutubi\__SERVICE_NAME__ __VERSION__"

SectionEnd
