@echo off

@setlocal

set DEFAULT_PULSE_HOME=%~dp0..
if "%PULSE_HOME%"=="" set PULSE_HOME=%DEFAULT_PULSE_HOME%

if exist "%PULSE_HOME%\bin\common.bat" goto havePulse

echo Could not find "%PULSE_HOME%\bin\common.bat", please
echo set PULSE_HOME
goto end

:havePulse

for %%i in ("%PULSE_HOME%\lib\boot-*.jar") do call "%PULSE_HOME%\bin\lcp.bat" %%i

if "%PULSE_OPTS%"=="" set PULSE_OPTS=-Xmx512m

"%PULSE_HOME%\bin\PulseService" -install "__SERVICE_NAME__" "%JAVA_HOME%\jre\bin\server\jvm.dll" ^
    -Djava.class.path="%LOCALCLASSPATH%" ^
    -Djava.util.logging.config.class=com.zutubi.pulse.logging.ConsoleConfig ^
    -Dpulse.home="%PULSE_HOME%" ^
    -Djava.awt.headless=true ^
    -start com.zutubi.pulse.command.PulseCtl ^
    -params start ^
    -stop com.zutubi.pulse.command.PulseCtl ^
    -params shutdown force ^
    -out "%PULSE_HOME%\stdout.txt" ^
    -err "%PULSE_HOME%\stderr.txt" ^
    -description "__SERVICE_DESCRIPTION__"

:end
rem all done

@endlocal
